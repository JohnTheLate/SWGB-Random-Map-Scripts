/* ********* ICE LAKE ********** */
/* 03 JUL 01 */

#include_drs random_map.def 54000

/* ****************************************************** */
<PLAYER_SETUP>
  random_placement  /* note this is only currently valid entry */

/* ****************************************************** */
<LAND_GENERATION>

base_terrain SNOW1

start_random
	percent_chance 25
	#define NABOO_FOREST
	percent_chance 25
	#define KASHYYYK
	percent_chance 25
	#define ENDOR
	percent_chance 25
	#define NABOO_GRASS
end_random

#define ICELAKE


/* CREATE THE ICE LAKE */

create_land 
{
	terrain_type                    ICE1
	land_percent                    80
	left_border                     15
	right_border                    20
	top_border                      15
	bottom_border                   20 
	border_fuzziness                40
	other_zone_avoidance_distance   2
	zone							16
}

create_player_lands 
{            
	terrain_type                    SNOW1
	land_percent                    30
	base_size						7
	zone 1
	other_zone_avoidance_distance   5 
	border_fuzziness                10
}

/* ****************************************************** */
<TERRAIN_GENERATION>

/* PRIMARY CARBON */

create_terrain TREE_E
{
	base_terrain                   SNOW1
	spacing_to_other_terrain_types 5
	land_percent                   10
	number_of_clumps               12
	set_avoid_player_start_areas     
	set_scale_by_groups
}

/* PRIMARY PATCH */

if NABOO_GRASS
	create_terrain GRASS4
else
	create_terrain SNOW_DIRT
endif
{
	base_terrain                   SNOW1
	number_of_clumps               12
	spacing_to_other_terrain_types 1
	land_percent                   11
	set_scale_by_size
}

/* SECONDARY PATCH */

if NABOO_GRASS
	create_terrain GRASS4
else
	create_terrain SNOW_DIRT
endif
{
	base_terrain                   SNOW1
	number_of_clumps               24
	spacing_to_other_terrain_types 1
	land_percent                   4
	set_scale_by_size
}

/* SECONDARY CARBON */

create_terrain TREE_E
{
	if NABOO_GRASS
		base_terrain                GRASS4
	else
		base_terrain				SNOW_DIRT
	endif
	spacing_to_other_terrain_types	3
	land_percent					1
	number_of_clumps				3
	set_avoid_player_start_areas     
	set_scale_by_groups
}

/* TERTIARY PATCH */

if NABOO_GRASS
	create_terrain GRASS4
else
	create_terrain SNOW_DIRT
endif
{
	if NABOO_GRASS
		base_terrain				GRASS4
	else
		base_terrain				SNOW_DIRT
	endif
	number_of_clumps				30
	spacing_to_other_terrain_types	1
	land_percent					2
	set_scale_by_size
}

/* ****************************************************** */
<OBJECTS_GENERATION>

#include_drs GBG_land_resources.inc 54103

if FOODB1
else
	create_object ANIMAL-NERF
	{
		if FOODB2
			number_of_objects			1
		elseif FOODB4
			number_of_objects			6
		else
			number_of_objects			3
		endif
		group_variance					1
		number_of_groups				2
		set_loose_grouping
		set_gaia_object_only
		set_place_for_every_player
		min_distance_to_players			35
		min_distance_group_placement	5
	}
endif

create_object OBJ-TREE_E
{
	number_of_objects          30
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_to_players    8
}

create_object ANIMAL-CU-PA
{
	number_of_objects			3
	number_of_groups			2
	set_loose_grouping
	set_gaia_object_only
	min_distance_to_players		15
}

/* ****************************************************** */

<ELEVATION_GENERATION>

create_elevation        7
{
	base_terrain		SNOW1
	number_of_clumps 	14
	number_of_tiles 	2000
	set_scale_by_groups
	set_scale_by_size           
}

<CLIFF_GENERATION>

min_number_of_cliffs 5
max_number_of_cliffs 8
min_length_of_cliff  4
max_length_of_cliff  10
cliff_curliness      10
min_distance_cliffs  3     


/* ****************************************************** */
<CONNECTION_GENERATION>
create_connect_all_players_land
/*
{
  replace_terrain GRASS1         SAND 
  replace_terrain WATER1         WATERSWAMP
  replace_terrain CARBOND        SAND
  replace_terrain CARBONE   SAND
  terrain_cost    WATER1         7
  terrain_cost    CARBOND        7
  terrain_cost    CARBONE   7
  terrain_cost    WATERSWAMP       3
  terrain_cost    SAND        1
  terrain_cost    GRASS1         2
  terrain_cost    SHORE         7
  terrain_size    WATER1         2       1
  terrain_size    GRASS1         0       0
  terrain_size    CARBOND        0       0
  terrain_size    CARBONE   0       0
  terrain_size    SAND        0       0
}
*/

