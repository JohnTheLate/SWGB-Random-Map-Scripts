#include_drs random_map.def 54000

/* ****************************************************** */
<PLAYER_SETUP>
random_placement

/* ****************************************************** */
<LAND_GENERATION>

#define HOTH

base_terrain    SNOW1
create_player_lands 
{ 
        terrain_type            SNOW1
        land_percent            25  
        base_size               9   
        border_fuzziness        15  
}

/* ****************************************************** */
<TERRAIN_GENERATION>
create_terrain METAL_CARBON
{
        base_terrain                    SNOW1
        spacing_to_other_terrain_types  5 
        land_percent                    12
        number_of_clumps                12
        set_avoid_player_start_areas    
        set_scale_by_groups             
}

create_terrain METAL_CARBON             
{                                       
        base_terrain                    SNOW1
        spacing_to_other_terrain_types  3 
        land_percent                    1 
        number_of_clumps                5 
        set_avoid_player_start_areas    
        set_scale_by_groups             
}

create_terrain SNOW_ROCK
{
        base_terrain                    SNOW1
        number_of_clumps                30
        spacing_to_other_terrain_types  1 
        land_percent                    10
        set_scale_by_size               
}                                       
  
create_terrain SNOW_METAL               
{                                       
        base_terrain                    SNOW1
        number_of_clumps                30
        spacing_to_other_terrain_types  1 
        land_percent                    10
        set_scale_by_size               
}                                       

/* ****************************************************** */
<OBJECTS_GENERATION>
#include_drs GBG_land_resources.inc 54103

if FOODB1
else
        create_object ANIMAL-BANTHA             
        {                                       
                if FOODB2                       
                        number_of_objects       2     
                elseif FOODB4                   
                        number_of_objects       8     
                else                            
                        number_of_objects       4     
                endif                           
                group_variance                  1     
                set_loose_grouping              
                set_gaia_object_only            
                set_place_for_every_player      
                min_distance_to_players         19    
        }
endif

create_object OBJ-CARBONB
{
        number_of_objects       30
        set_scaling_to_map_size 
        min_distance_to_players 8
}

create_object OBJ-ROCKS1
{
        number_of_objects       300
        min_distance_to_players 15
}

start_random
        percent_chance 50
        #define M1
end_random

start_random
        percent_chance 75
        if M1
                create_object SNOWMOUNTAIN1
        else
                create_object SNOWMOUNTAIN2
        endif
        {
                number_of_objects       1
                min_distance_to_players 40
        }
end_random

start_random
        percent_chance 50
        #define M2
end_random

start_random
        percent_chance 25
        if M2
                create_object SNOWMOUNTAIN2
        else
                create_object SNOWMOUNTAIN1
        endif
        {
                number_of_objects       1
                min_distance_to_players 40
        }
end_random


/* ****************************************************** */
<ELEVATION_GENERATION>
create_elevation        7
{
        base_terrain            SNOW1
        number_of_clumps        20   
        number_of_tiles         6000 
        set_scale_by_groups     
        set_scale_by_size       
}

/* ****************************************************** */
<CLIFF_GENERATION>
min_number_of_cliffs 10
max_number_of_cliffs 15
min_length_of_cliff  3
max_length_of_cliff  10

