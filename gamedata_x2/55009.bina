/* ************ TATOOINE ************ */
/* 23 JAN 02 */

#include_drs random_map.def 54000

/* ****************************************************** */
<PLAYER_SETUP>
random_placement

/* ****************************************************** */
<LAND_GENERATION>

#define TATOOINE	 

base_terrain	SAND1

create_player_lands 
{ 
	terrain_type		SAND1
	land_percent        25
	base_size           9
	border_fuzziness    15
}

/* ****************************************************** */
<TERRAIN_GENERATION>

/* PRIMARY CARBON */

create_terrain ROCK_CARBON
{
	base_terrain					SAND1
	spacing_to_other_terrain_types	5
	land_percent					12
	number_of_clumps				12
	set_avoid_player_start_areas     
	set_scale_by_groups
}

/* PRIMARY PATCH */

create_terrain SAND2
{
	base_terrain					SAND1
	number_of_clumps				16
	spacing_to_other_terrain_types	0
	land_percent					20
	set_scale_by_size
}

/* SECONDARY CARBON */

create_terrain ROCK_CARBON
{
	base_terrain					SAND1
	spacing_to_other_terrain_types	3
	land_percent					1
	number_of_clumps				5
	set_avoid_player_start_areas     
	set_scale_by_groups
}

/* SECONDARY PATCH */

create_terrain SAND_ROCK
{
	base_terrain					SAND1
	number_of_clumps				24
	spacing_to_other_terrain_types	1
	land_percent					2
	set_scale_by_size
}

/* TERTIARY PATCH */

create_terrain SAND2
{
	base_terrain					SAND1
	number_of_clumps				30
	spacing_to_other_terrain_types	1
	land_percent					2
	set_scale_by_size
}

/* ****************************************************** */
<OBJECTS_GENERATION>

#include_drs GBG_land_resources.inc 54103

create_object TATOOINE_DOME
{
	number_of_objects				1
	set_gaia_object_only
	min_distance_to_players			35
	min_distance_group_placement	15
}

create_object TATOOINE_VAPORATOR
{
	number_of_objects				5
	set_gaia_object_only
	min_distance_to_players			35
	min_distance_group_placement	15
}

create_object TATOOINE_HOUSEA
{
	number_of_objects				1
	number_of_groups				10
	set_gaia_object_only
	min_distance_to_players			35
	min_distance_group_placement	15
}

create_object TATOOINE_HOUSEB
{
	number_of_objects				1
	number_of_groups				10
	set_gaia_object_only
	min_distance_to_players			35
	min_distance_group_placement	15
}

create_object TATOOINE_TOWER
{
	number_of_objects				10
	set_gaia_object_only
	min_distance_to_players			35
	min_distance_group_placement	15
}

if FOODD1
else
	create_object ANIMAL-EOPIE
	{
		if FOODD2
			number_of_objects		2
		elseif FOODD4
			number_of_objects		8
		else
			number_of_objects		4
		endif
		group_variance				1
		set_loose_grouping
		set_gaia_object_only
		set_place_for_every_player
		min_distance_to_players		19
	}
endif

create_object OBJ-ROCKS2
{
	number_of_objects				140
	set_gaia_object_only
	min_distance_to_players			15
}

if TINY_MAP

create_object SANDCRAWLER
{
	number_of_objects				1
	number_of_groups				2
	set_gaia_object_only
	min_distance_to_players			10
}

else

create_object SANDCRAWLER
{
	number_of_objects				1
	number_of_groups				4
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_to_players			20
}

endif

start_random
	percent_chance 75
	create_object MOUNTAIN4
	{
		number_of_objects	1
		set_gaia_object_only
		min_distance_to_players		40
	}
end_random

start_random
	percent_chance 25
	create_object MOUNTAIN4
	{
		number_of_objects	1
		set_gaia_object_only
		min_distance_to_players	40
	}
end_random

/* ****************************************************** */

<ELEVATION_GENERATION>

create_elevation        7
{
	base_terrain		SAND1
	number_of_clumps 	20
	number_of_tiles 	6000
	set_scale_by_groups
	set_scale_by_size           
}

/* ****************************************************** */
<CLIFF_GENERATION>
min_number_of_cliffs 10
max_number_of_cliffs 15
min_length_of_cliff  3
max_length_of_cliff  10
